plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.sonarqube" version "7.1.0.6387"
    id 'jacoco'
    id "com.diffplug.spotless" version "6.25.0"
}

group = 'btc.exchange'
version = '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories { mavenCentral() }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport 
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "4TT1L4_trading-engine"
        property "sonar.organization", "bujakiattila"

        property "sonar.coverage.jacoco.xmlReportPaths",
                "${layout.buildDirectory.get()}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

spotless {
    java {
        target "src/**/*.java"
        googleJavaFormat("1.22.0")
        trimTrailingWhitespace()
        endWithNewline()
        removeUnusedImports()
    }
}

tasks.named("check") {
    dependsOn("spotlessCheck")
}